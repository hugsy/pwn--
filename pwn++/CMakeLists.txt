project(pwn++ LANGUAGES CXX VERSION 0.1.3)
message(STATUS "Configuring library '${PROJECT_NAME}'")

set(PWN_LIBRARY_NAME ${PROJECT_NAME})
set(PWN_LIBRARY_AUTHOR ${PROJECT_AUTHOR})
set(PWN_LIBRARY_LICENSE ${PROJECT_LICENSE})
set(PWN_LIBRARY_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(PWN_LIBRARY_VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(PWN_LIBRARY_VERSION_PATCH ${PROJECT_VERSION_PATCH})
set(PWN_LIBRARY_VERSION_RELEASE "Standalone")
set(PWN_LIBRARY_VERSION ${PROJECT_VERSION})
set(PWN_BUILD_ARCHITECTURE ${CMAKE_SYSTEM_PROCESSOR})
set(PWN_BUILD_OS ${CMAKE_SYSTEM_NAME})

set(PWN_MODULES_AS_STRING "")
foreach(MODULE ${PWN_MODULES})
    set(PWN_MODULES_AS_STRING "L\"${MODULE}\",${PWN_MODULES_AS_STRING}")
endforeach()

set(INTERFACE_DIR ${PROJECT_SOURCE_DIR}/Include)
set(SOURCE_DIR ${PROJECT_SOURCE_DIR}/Source)
set(HEADER_DIR ${SOURCE_DIR}/Include)

if(BUILD_SHARED_LIBS)
    add_library(${PROJECT_NAME} SHARED)
else()
    add_library(${PROJECT_NAME} STATIC)
endif()

message(STATUS "Generating pwn++ main header")
list(TRANSFORM PWN_MODULES PREPEND PWN:: OUTPUT_VARIABLE MODULE_NAMESPACES)
list(LENGTH MODULE_NAMESPACES PWN_MODULES_LENGTH)
configure_file(${PROJECT_SOURCE_DIR}/pwn.hpp.in ${INTERFACE_DIR}/pwn NEWLINE_STYLE WIN32)

if(MSVC)
    target_sources(${PROJECT_NAME} PRIVATE ${SOURCE_DIR}/Win32/dllmain.cpp)

    target_link_options(
        ${PROJECT_NAME}

        PRIVATE
        $<TARGET_PROPERTY:${PROJECT_NAME},INTERFACE_LINK_OPTIONS>
        $<$<CONFIG:Debug>:/DEBUG:FULL> /WX /RELEASE /SUBSYSTEM:Console /MANIFEST:NO /DLL
        /profile /guard:cf /DEBUGTYPE:cv,fixup /LARGEADDRESSAWARE
    )

else()
    target_sources(${PROJECT_NAME} PRIVATE ${SOURCE_DIR}/Linux/dllmain.cpp)

    target_link_libraries(
        ${PROJECT_NAME}

        LINK_PUBLIC
        pthread
        dl
    )
endif(MSVC)

target_include_directories(${PROJECT_NAME} PUBLIC ${INTERFACE_DIR} PRIVATE ${HEADER_DIR})
target_link_libraries(${PROJECT_NAME} PUBLIC ${MODULE_NAMESPACES})
install(TARGETS ${PROJECT_NAME} DESTINATION ${PROJECT_NAME}/Library)
